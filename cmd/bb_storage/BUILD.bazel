load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//tools:container.bzl", "container_push_official")

go_library(
    name = "bb_storage_lib",
    srcs = ["main.go"],
    importpath = "github.com/buildbarn/bb-storage/cmd/bb_storage",
    visibility = ["//visibility:private"],
    deps = [
        "//pkg/auth",
        "//pkg/blobstore",
        "//pkg/blobstore/configuration",
        "//pkg/blobstore/grpcservers",
        "//pkg/builder",
        "//pkg/capabilities",
        "//pkg/global",
        "//pkg/grpc",
        "//pkg/program",
        "//pkg/proto/configuration/bb_storage",
        "//pkg/proto/fsac",
        "//pkg/proto/icas",
        "//pkg/proto/iscc",
        "//pkg/util",
        "@com_github_bazelbuild_remote_apis//build/bazel/remote/execution/v2:execution",
        "@org_golang_google_genproto_googleapis_bytestream//:bytestream",
        "@org_golang_google_grpc//:go_default_library",
        "@org_golang_google_grpc//codes",
        "@org_golang_google_grpc//status",
    ],
)

[
    go_binary(
        name = "bb_storage-{}".format(goarch),
        embed = [":bb_storage_lib"],
        pure = "on",
        goarch = goarch,
        goos = "linux",
        visibility = ["//visibility:public"],
    )
    for goarch in ["amd64", "arm64"]
]

[
    pkg_tar(
        name = "tar-{}".format(goarch),
        srcs = ["bb_storage-{}".format(goarch)],
        package_dir = "/app/cmd/bb_storage",
    )
    for goarch in ["amd64", "arm64"]
]

[
    oci_image(
        name = "image-{}".format(goarch),
        base = "@distroless_static",
        entrypoint = ["/app/cmd/bb_storage/bb_storage-{}".format(goarch)],
        tars = ["tar-{}".format(goarch)],
    )
    for goarch in ["amd64", "arm64"]
]

oci_image_index(
    name = "bb_storage_container",
    images = [
        ":image-arm64",
        ":image-amd64",
    ],
)

container_push_official(
    name = "bb_storage_container_push",
    component = "bb-storage",
    image = ":bb_storage_container",
)
